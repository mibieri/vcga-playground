extension Parcels --> Parcels.
extension Surroundings --> color("#CCFFCC") Surroundings.

@Order(1)
@Range(min=1, max=100, stepsize=0.1, restricted=false)
attr Footprint_Size_X = 20

@Order(2)
@Range(min=1, max=100, stepsize=0.1, restricted=false)
attr Footprint_Size_Z = 20

@Order(3)
@Range(min=0, max=100, stepsize=0.1, restricted=false)
attr Row_Inner_Distance = 40

@Order(4)
@Range(min=0, max=100, stepsize=0.1, restricted=false)
attr Row_Border_Distance = 20

@Order(5)
@Range(min=1, max=10, stepsize=1, restricted=false)
attr Parcels_Per_Group = 2

@Order(6)
@Range(min=0, max=100, stepsize=0.1, restricted=false)
attr Group_Inner_Distance = 30

@Order(7)
@Range(min=0, max=100, stepsize=0.1, restricted=false)
attr Group_Border_Distance = 15

@Order(8)
@Angle
attr Grid_Rotation = 0


	
start Start -->
	rotateScope(0, Grid_Rotation, 0)
	SplitIntoRows


SplitIntoRows-->
	split(z, noAdjust){
		Row_Border_Distance: NIL |
		{ Footprint_Size_Z: SplitRowIntoParcels |  ~Row_Inner_Distance: NIL} * |
		Footprint_Size_Z: SplitRowIntoParcels |
		Row_Border_Distance: NIL
	}


SplitRowIntoParcels -->
	split(x, noAdjust){
		Group_Border_Distance: NIL |
		{ Footprint_Size_X * Parcels_Per_Group: SplitGroupsIntoParcels |  ~Group_Inner_Distance: NIL} * |
		Footprint_Size_X * Parcels_Per_Group: SplitGroupsIntoParcels |
		Group_Border_Distance: NIL
	}


	
SplitGroupsIntoParcels -->
	split(x){
		~Footprint_Size_X: Parcels
	}*
		