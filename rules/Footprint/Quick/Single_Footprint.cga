extension Footprint --> Footprint.
extension Surroundings --> NIL


@Order(0)
@Handle(Shape=FootprintBoundingBox, type=selector)
@Enum(valuesAttr=footprintShapes)
attr Shape = label_shape_rectangle

@Order(1)
@Range(min=0, max=100, restricted=false)
attr SizeX = 20

@Order(2)
@Range(min=0, max=100, restricted=false)
attr SizeZ = 20

@Order(3)
@Angle
attr Rotation = 0

@Order(4)
@Percent
attr PositionX = 0.5

@Order(5)
@Percent
attr PositionZ = 0.5

@Order(6)
@Range(min=0, max=30, stepsize=0.1, restricted=false)
attr SlabThickness = 10


const label_shape_rectangle = "Rectangle"
const label_shape_ellipse = "Ellipse"
const label_shape_cross = "Cross"
const label_shape_amoeba = "Amoeba"
const label_shape_silos = "Silos"

@Hidden
attr footprintShapes = [
	label_shape_rectangle, label_shape_ellipse, label_shape_cross, label_shape_amoeba, label_shape_silos
]


start Start -->
	Surroundings
	primitiveQuad()
	t(scope.sx * PositionX, 0, scope.sz * PositionZ)
	t(-SizeX / 2, 0, -SizeZ / 2)
	s(SizeX, 0, SizeZ)
	r(scopeCenter, 0, Rotation, 0)
	FootprintBoundingBox
	
	
FootprintBoundingBox -->
	case Shape == label_shape_rectangle:
		Footprint
	case Shape == label_shape_ellipse:
		primitiveDisk(64)
		Footprint
	case Shape == label_shape_cross:
		CrossFootprint
	case Shape == label_shape_amoeba:
		AssetFootprint("footprint_amoeba.glb")
	case Shape == label_shape_silos:
		AssetFootprint("footprint_silo.glb")
	else:
		NIL


CrossFootprint -->
	split(x, noAdjust){
		~1: CrossFootprint_Rows |
		SlabThickness: Footprint |
		~1: CrossFootprint_Rows
	}


CrossFootprint_Rows -->
	split(z, noAdjust){
		~1: Surroundings |
		SlabThickness: Footprint |
		~1: Surroundings
	}
	

AssetFootprint(assetFileName) -->
	i(assetFileName)
	cleanupGeometry(all, 0.1)
	Footprint